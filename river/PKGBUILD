developer=http://scripting.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.5.15
pkgrel=3
_commit=403c5ec3df59cd95c44f28622e8c9944260ba6d0
pkgdesc="A river-of-news RSS aggregator"
arch=('any')
license=("custom:river")
source=("git+https://github.com/scripting/river5#commit=${_commit}")
releasepage=('https://github.com/scripting/river5/commits/master')
depends=(
    'nodejs'
)
options=('!strip')
sha512sums=('SKIP')

build() {
    cd river5
    npm install

    # npm puts those _where fields everywhere in to package.json, and makepkg does not
    # like those absolute paths in there. We don't either. So let's take them out:
    find . -name package.json -exec perl -pi -e 's,^\s*"_where".*$,,g' {} \;
    find . -name package.json -exec perl -pi -e "s,$(pwd).*node_modules,/ubos/share/${pkgname}/mode_modules,g" {} \;
}

package() {
# Manifest
   install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Data directory
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Systemd
    install -D -m0644 ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/

    install -D -m0644 ${startdir}/systemd/sysusers ${pkgdir}/usr/lib/sysusers.d/river.conf

# Templates
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl

# Code
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/bin
    cp -r -p ${srcdir}/river5/* ${pkgdir}/ubos/share/${pkgname}/
    install -D -m755 ${startdir}/bin/* -t ${pkgdir}/ubos/share/${pkgname}/bin/

# License
    install -D -m644 ${srcdir}/river5/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
