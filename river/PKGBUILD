developer=http://scripting.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.5
pkgrel=4
_commit=68a89ff9c7b2dfb1e3b533580eddd52326496a01
pkgdesc="A river-of-news RSS aggregator"
arch=('any')
license=("custom:river")
source=("git+https://github.com/scripting/river5#commit=${_commit}")
releasepage=('https://github.com/scripting/river5/commits/master')
depends=(
    'nodejs'
)
options=('!strip')
sha512sums=('SKIP')
install=river.install

build() {
    cd river5
    npm install

    # npm puts those _where fields everywhere in to package.json, and makepkg does not
    # like those absolute paths in there. We don't either. So let's take them out:
    find . -name package.json -exec perl -pi -e 's,^\s*"_where".*$,,g' {} \;
    find . -name package.json -exec perl -pi -e "s,$(pwd).*node_modules,/usr/share/${pkgname}/mode_modules,g" {} \;
}

package() {
# Manifest
   mkdir -p ${pkgdir}/var/lib/ubos/manifests
   install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
#    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
#    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Systemd
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    cp ${startdir}/systemd/*.service ${pkgdir}/usr/lib/systemd/system/

    mkdir -p ${pkgdir}/usr/lib/sysusers.d
    cp ${startdir}/systemd/sysusers ${pkgdir}/usr/lib/sysusers.d/river

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    cp ${startdir}/tmpl/*.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    cp -r -p ${srcdir}/river5/* ${pkgdir}/usr/share/${pkgname}/
    install -m755 ${startdir}/bin/* -t ${pkgdir}/usr/share/${pkgname}/bin

# License
    mkdir -p ${pkgdir}/usr/share/licenses
    install -m644 ${srcdir}/river5/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}
}
